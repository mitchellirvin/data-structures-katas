buildscript {
    repositories {
        mavenCentral()
    }
    configurations.maybeCreate("pitest")
    dependencies {
        pitest 'org.pitest:pitest-junit5-plugin:0.10'
    }
}

plugins {
    id 'java-library'
    id 'info.solidsoft.pitest' version '1.4.5'
    id 'jacoco'
}

group 'katas.datastructures'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = '0.8.4'
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            includes = ['katas.datastructures.*']
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 1
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
}
test.finalizedBy jacocoTestReport

pitest {
    testPlugin = "junit5"
    targetClasses = ['katas.datastructures.*']
    threads = 4
    outputFormats = ['HTML']
    mutationThreshold = 100
}
check.dependsOn 'pitest'
